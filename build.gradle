plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

allprojects {
    apply plugin: "kotlin"

    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://repo.incendo.org/content/repositories/snapshots/' }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

        testImplementation group: 'junit', name: 'junit', version: '4.12'

        api group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    }

    sourceSets {
        // force usage of `java` folder for kotlin sources
        main.kotlin.srcDirs += 'src/main/java'
        test.kotlin.srcDirs += 'src/test/java'
    }

    group = 'com.dongtronic.diabot'
    version = '2.0.0'

    sourceCompatibility = 11
    targetCompatibility = 11

    // from: https://kotlinlang.org/docs/reference/using-gradle.html
    //noinspection GroovyAssignabilityCheck,GrUnresolvedAccess
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        //noinspection GrUnresolvedAccess
        kotlinOptions {
            jvmTarget = "11"
            freeCompilerArgs += '-java-parameters'
        }
    }
}

dependencies {
    implementation project(path: ':bot')
}

task stage(dependsOn: ['clean', 'shadowJar'])

shadowJar {
    exclude 'logback-test.xml'
    archiveBaseName.set('diabot')
    archiveClassifier.set('')
    archiveVersion.set('')
    manifest {
        attributes(
                'Implementation-Title': 'Diabot - a diabetes Discord bot',
                'Implementation-Version': this.version,
                'Main-Class': 'com.dongtronic.diabot.Main',
                // fixes retrofit v2.8 reflection warnings
                'Add-Opens': 'java.base/java.lang.invoke'
        )
    }
}