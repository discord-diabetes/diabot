plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version "${kotlinVersion}"
    id 'com.github.johnrengelman.shadow' version "${shadowVersion}"
    id 'io.gitlab.arturbosch.detekt' version "${detektVersion}"
    id 'net.researchgate.release' version "${releasePluginVersion}"
}

allprojects {
    apply plugin: "kotlin"

    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://m2.dv8tion.net/releases' }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"
        implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${kotlinCoroutinesVersion}"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-reactor:${kotlinCoroutinesVersion}"
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "${junitVersion}"
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: "${junitVersion}"
        api group: 'org.slf4j', name: 'slf4j-api', version: "${slf4jVersion}"
    }

    test {
        useJUnitPlatform()
        systemProperty "junit.jupiter.testinstance.lifecycle.default", "per_class"
    }

    group = 'com.dongtronic.diabot'

    sourceCompatibility = 11
    targetCompatibility = 11

    // from: https://kotlinlang.org/docs/reference/using-gradle.html
    //noinspection GroovyAssignabilityCheck,GrUnresolvedAccess
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        //noinspection GrUnresolvedAccess
        kotlinOptions {
            jvmTarget = "11"
        }
    }

    detekt {
        buildUponDefaultConfig = true // preconfigure defaults
        allRules = false // activate all available (even unstable) rules.
        config = files("$rootDir/config/detekt/detekt.yml")
        // point to your custom config defining rules to run, overwriting default behavior
        baseline = file("$rootDir/config/detekt/baseline.xml")

        source = objects.fileCollection().from(
                "src/main/java",
                "src/test/java"
        )

        reports {
            html.enabled = true // observe findings in your browser with structure and code snippets
            xml.enabled = true // checkstyle like format mainly for integrations like Jenkins
            txt.enabled = true
            // similar to the console output, contains issue signature to manually edit baseline files
            sarif.enabled = true
            // standardized SARIF format (https://sarifweb.azurewebsites.net/) to support integrations with Github Code Scanning
            sarif {
                enabled = true
                destination = file("$rootDir/detekt.sarif.json")
            }
        }
    }
}

task stage(dependsOn: ['clean', 'shadowJar'])

shadowJar {
    exclude 'logback-test.xml'
    archiveBaseName.set('diabot')
    archiveClassifier.set('')
    archiveVersion.set('')
    manifest {
        attributes(
                'Implementation-Title': 'Diabot - a diabetes Discord bot',
                'Implementation-Version': this.version,
                'Main-Class': 'com.dongtronic.diabot.Main',
                // fixes retrofit v2.8 reflection warnings
                'Add-Opens': 'java.base/java.lang.invoke'
        )
    }
}

release {
    tagTemplate = 'v$version'
    git {
        requireBranch = 'main'
        signTag = false
    }
}

dependencies {
    implementation project(path: ':bot')
}
